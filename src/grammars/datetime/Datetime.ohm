Datetime {
  Expression
    = Addition
    | Subtraction
    | TimeUnitUntilDate
    | TimeUnitSinceDate
    | Primitive

  Addition
    = Primitive add Timespan

  Subtraction
    = Primitive subtract Timespan
  
  Primitive
    = Datetime
    | Now
    | Today
    | Tomorrow
    | Yesterday
    | NextDayOfWeek
    | LastDayOfWeek

  Timespan
    = number unitsOfTime

  Now 
    = now

  Today 
    = today

  Tomorrow 
    = tomorrow

  Yesterday 
    = yesterday
  
  // e.g. 1/10/23 or 01.10.2023
  Datetime 
  	= datetimeIso8601
    | datetimeFormatEuropeanOrUs
  
  // e.g. Next Monday
  NextDayOfWeek
    = caseInsensitive<"next"> dayOfWeek
  
  // e.g. Last Tuesday
  LastDayOfWeek
    = caseInsensitive<"last"> dayOfWeek
    
  TimeUnitUntilDate
  	= (year | month | week | day) until Datetime
    
  TimeUnitSinceDate
  	= (year | month | week | day) since Datetime

  // Lexical Rules
  oneDigit
    = digit

  twoDigit 
    = digit digit
  
  fourDigit
    = digit digit digit digit

  time
    =  (twoDigit | oneDigit) ":" (twoDigit | oneDigit) ":" (twoDigit | oneDigit)

  datetimeIso8601
    = fourDigit "-" twoDigit "-" twoDigit "T"? (twoDigit ":" twoDigit ":" twoDigit)? ("." digit+)? ("Z" | timeZone)?

  datetimeFormatEuropeanOrUs
    = (twoDigit | oneDigit) "/" (twoDigit | oneDigit) "/" (fourDigit | twoDigit) time?
    | (twoDigit | oneDigit) "-" (twoDigit | oneDigit) "-" (fourDigit | twoDigit) time?
    | (twoDigit | oneDigit) "." (twoDigit | oneDigit) "." (fourDigit | twoDigit) time?
    
  timeZone
 	 = "+" digit+ ":" digit+
 	 | "-" digit+ ":" digit+

  add 
  	= "+"
  	| caseInsensitive<"add">
    | caseInsensitive<"plus">

  subtract 
    = "-"
    | caseInsensitive<"take">
    | caseInsensitive<"minus">
    | caseInsensitive<"subtract">

  now
    = caseInsensitive<"now">

  today
    = caseInsensitive<"today">

  tomorrow
    = caseInsensitive<"tomorrow">

  yesterday
    = caseInsensitive<"yesterday">

  dayOfWeek
    = monday
    | tuesday
    | wednesday
    | thursday
    | friday
    | saturday
    | sunday

  monday
    = caseInsensitive<"monday">

  tuesday
    = caseInsensitive<"tuesday">

  wednesday
    = caseInsensitive<"wednesday">

  thursday
    = caseInsensitive<"thursday">
  
  friday
    = caseInsensitive<"friday">

  saturday
    = caseInsensitive<"saturday">

  sunday
    = caseInsensitive<"sunday">

  unitsOfTime
    = year
    | month
    | week
    | day
    | hour
    | minute
    | second
    | millisecond

  year
    = caseInsensitive<"years">
    | caseInsensitive<"year">
    | "y"

  month
    = caseInsensitive<"months">
    | caseInsensitive<"month">
    | "M"

  week
    = caseInsensitive<"weeks">
    | caseInsensitive<"week">
    | "w"

  day
    = caseInsensitive<"days">
    | caseInsensitive<"day">
    | "d"

  hour
    = caseInsensitive<"hours">
    | caseInsensitive<"hour">
    | "h"

  minute
    = caseInsensitive<"minutes">
    | caseInsensitive<"minute">
    | "m"
    
  second
    = caseInsensitive<"seconds">
    | caseInsensitive<"second">
    | "s"

  millisecond
    = caseInsensitive<"milliseconds">
    | caseInsensitive<"millisecond">
    | "ms"
    
  until
  	= "until"
    | "before"
    
  since
  	= "since"
    | "after"

  number
    = digit* "." digit+  -- fract
    | digit+             -- whole
    
  integer
  	= digit+
} 